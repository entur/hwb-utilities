buildscript {
    ext.repos = {
        mavenLocal()
        mavenCentral()
        google()
        maven {
            url "https://javacard.pro/maven/"
            content {
                includeGroup "com.github.martinpaljak"
            }
        }

    }

    repositories repos
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.51.0' apply false // ./gradlew dependencyUpdates
    id('io.github.gradle-nexus.publish-plugin') version '2.0.0'
}

def buildProjects() {
    subprojects.findAll { new File(it.projectDir, 'build.gradle').file }
}

def javaLibraryProjects() {
    subprojects.findAll { it.name.endsWith("-java-lib")}
}

def androidLibraryProjects() {
    subprojects.findAll { it.name.endsWith("-android-lib")}
}


allprojects {
    // skip checking test configurations (like lint) or test libraries for vulnerabilities
    buildscript {
        repositories repos
    }
    repositories repos
}

ext {
    jacksonVersion = '2.20.1'
    junitVersion = '4.13.2'
    javaWebSocketVersion = '1.6.0'
    hivemqMqttClientVersion = '1.2.1'
    nettyVersion = '4.1.48.Final'
}

ext.commonDependencies = {

}

configure(buildProjects()) {
    apply plugin: 'com.github.ben-manes.versions'

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}


// build config
configure(androidLibraryProjects()) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 35
        testOptions.unitTests.includeAndroidResources = true

        defaultConfig {
            // set minSdkVersion per subproject
            targetSdkVersion 35
            // versions are copied here from the properties to improve build time. See the version.gradle script.
            versionCode Integer.parseInt(project.findProperty('versionCode'))
            versionName project.findProperty('version')
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

                crunchPngs false
            }

            debug {
                debuggable true
            }
        }

        compileOptions {
            // Sets Java compatibility to Java 8
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        lintOptions {
            // disable lint in projects
            // add lint in app, including dependencies
            tasks.lint.enabled = false
        }

        testOptions {
            unitTests {
                all {
                    maxHeapSize = "4g"
                }
                returnDefaultValues = true
            }
        }

        buildFeatures {
            buildConfig = false
        }

        packagingOptions {
            exclude 'META-INF/INDEX.LIST'
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.md'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/LICENSE-notice.md'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/notice.txt'
            exclude 'META-INF/ASL2.0'
        }
    }

    dependencies commonDependencies
}


// quality control + release
configure(androidLibraryProjects()) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    android {
        publishing {
            singleVariant('release') {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.findByName('release')

                    groupId "$groupId"
                    artifactId "${!project.ext.has('artifactId') ? project.name : project.ext.artifactId}"
                    version = android.defaultConfig.versionName

                    pom {
                        name = "${!project.ext.has('artifactId') ? project.name : project.ext.artifactId}"
                        description = 'HWB utilities'
                        url = 'https://github.com/entur/hwb-utilities'
                        packaging = 'aar'
                        inceptionYear = '2025'
                        licenses {
                            license {
                                name = 'European Union Public Licence v1.2'
                                url = 'https://www.eupl.eu/'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                id = "skjolber"
                                name = "Thomas Skjølberg"
                                email = "thomas.rorvik.skjolberg@entur.org"
                            }
                        }
                        scm {
                            url = 'https://github.com/entur/hwb-utilities'
                            connection = 'git@github.com:entur/hwb-utilities.git'
                        }
                    }
                }
            }
        }
        if(project.hasProperty("signing.gnupg.keyName")) {
            signing {
                useGpgCmd()

                // set
                // signing.gnupg.keyName=xxx
                // signing.gnupg.passphrase=yyy
                // via command line

                sign publishing.publications.release
            }
        }
    }
}


configure(javaLibraryProjects()) {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories repos

    java {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        withJavadocJar()
        withSourcesJar()
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    afterEvaluate {
        publishing {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from components.java

                    groupId "$groupId"
                    artifactId "${!project.ext.has('artifactId') ? project.name : project.ext.artifactId}"
                    version = "$version"

                    pom {
                        name = "${!project.ext.has('artifactId') ? project.name : project.ext.artifactId}"
                        description = 'HWB utilities'
                        url = 'https://github.com/entur/hwb-utilities'
                        packaging = 'jar'
                        inceptionYear = '2025'
                        licenses {
                            license {
                                name = 'European Union Public Licence v1.2'
                                url = 'https://www.eupl.eu/'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                id = "skjolber"
                                name = "Thomas Skjølberg"
                                email = "thomas.rorvik.skjolberg@entur.org"
                            }
                        }
                        scm {
                            url = 'https://github.com/entur/hwb-utilities'
                            connection = 'git@github.com:entur/hwb-utilities.git'
                        }
                    }
                }
            }
            if(project.hasProperty("signing.gnupg.keyName")) {
                signing {
                    useGpgCmd()

                    // set
                    // signing.gnupg.keyName=xxx
                    // signing.gnupg.passphrase=yyy
                    // via command line

                    sign publishing.publications.release
                }
            }
        }

    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter', 'junit-vintage'
        }

    }
}

nexusPublishing {
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}


